function __init__citeseer()
    DEPNAME = "CiteSeer"
    LINK = "https://github.com/kimiyoung/planetoid/raw/master/data"
    DOCS = "https://github.com/kimiyoung/planetoid"
    DATA = "ind.citeseer." .* ["x", "y", "tx", "allx", "ty", "ally", "graph", "test.index"]

    register(DataDep(DEPNAME,
                     """
                     Dataset: The $DEPNAME dataset.
                     Website: $DOCS
                     """,
                     map(x -> "$LINK/$x", DATA),
                     "7f7ec4df97215c573eee316de35754d89382011dfd9fb2b954a4a491057e3eb3"  # if checksum omitted, will be generated by DataDeps
                     # post_fetch_method = unpack
                     ))
end

"""
    CiteSeer(; dir=nothing)

The CiteSeer citation network dataset from Ref. [1].
Nodes represent documents and edges represent citation links.
The dataset is designed for the node classification task. 
The task is to predict the category of certain paper.
The dataset is retrieved from Ref. [2].

# References

[1]: [Deep Gaussian Embedding of Graphs: Unsupervised Inductive Learning via Ranking](https://arxiv.org/abs/1707.03815) 
 
[2]: [Planetoid](https://github.com/kimiyoung/planetoid)
"""
struct CiteSeer <: AbstractDataset
    metadata::Dict{String, Any}
    graphs::Vector{Graph}
end

function CiteSeer(; dir = nothing, reverse_edges = true)
    metadata, g = read_planetoid_data("CiteSeer", dir = dir, reverse_edges = reverse_edges)
    return CiteSeer(metadata, [g])
end

Base.length(d::CiteSeer) = length(d.graphs)
Base.getindex(d::CiteSeer, ::Colon) = d.graphs[1]
Base.getindex(d::CiteSeer, i) = d.graphs[i]

# DEPRECATED in v0.6.0
function Base.getproperty(::Type{CiteSeer}, s::Symbol)
    if s === :dataset
        @warn "CiteSeer.dataset() is deprecated, use `CiteSeer()` instead."
        function dataset(; dir = nothing)
            d = CiteSeer(; dir)
            g = d[1]
            adjacency_list = edgeindex2adjlist(g.edge_index..., g.num_nodes)
            return (; g.num_nodes, g.num_edges,
                    node_features = g.node_data.features,
                    node_labels = g.node_data.targets,
                    train_indices = mask2indexes(g.node_data.train_mask),
                    val_indices = mask2indexes(g.node_data.val_mask),
                    test_indices = mask2indexes(g.node_data.test_mask),
                    adjacency_list)
        end
        return dataset
    else
        return getfield(CiteSeer, s)
    end
end
